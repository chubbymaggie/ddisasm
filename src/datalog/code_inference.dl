//===- code_inference.dl ----------------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
This module computes the valid instructions organized in blocks of
code.  It infers the facts 'code_in_block', 'block', and 'code'. In
addition it computes 'block_still_overlap' for debugging purposes.

The disassembly is done in two traversals:

  - A "must" backward traversal that propagates invalid instructions
  - A "may" forward traversal that tries to generate a
    superset of all possible code blocks (with the predicates possible_target
	and code_in_block_candidate). This traversal has a recursive component
	and a linear-sweep component.

Finally, we detect candidate blocks of code that overlap with each other (and
therefore are in conflict) and give them points according to several
heuristics. Then for each two conflicting blocks we keep the one with
more points.
*/


// These are final addresses where the code is located
// They are organized in blocks of sequential code
// The block identifier is the address of the first instruction in the block
.decl code_in_block(ea:address,block:address)
.decl block(block:address)
.decl code(ea:address)
.decl block_last_instruction(block:address,EA:address)

 // The postprocess refines the blocks by merging contiguous blocks
 // that are always executed together
#include "code_inference_postprocess.dl"

// cfg generates a control flow graph
#include "cfg.dl"

// we detect if there are still overlaping blocks,
// in that case the analysis has failed to resolve all conflicts
.decl block_still_overlap(ea:address,ea2:address)
.output block_still_overlap

/////////////////////////////////////////////////////////////
// Basic facts to define fallthrough

.decl may_fallthrough(o:address,d:address)

may_fallthrough(From,To):-
	next(From,To),
	!return(From),
	!unconditional_jump(From),
	!halt(From),
	!no_return_call_basic(From).
		
.decl must_fallthrough(o:address,d:address)

must_fallthrough(From,To):-
	may_fallthrough(From,To),
	instruction_get_operation(From,Operation),
    Operation!="CALL",
    !interrupt_operation(Operation),
	!instruction_has_loop_prefix(From),
    !conditional_jump(From).


//////////////////////////////////////////////////////////////
// This is a small refinement for discarding immediates as targets
// in some obvious cases. This is specially useful for PIE code where
// the address space starts from 0 (and therefore we have many more collisions).

.decl may_have_symbolic_immediate(src:address,dest:address)

may_have_symbolic_immediate(Src,Dest):-
    instruction_immediate_offset(Src,Index,Offset),
    instruction_get_op(Src,Index,Op),
	op_immediate(Op,Dest),
    (binary_type("DYN"),
     relocation(Src+Offset,_,_,_)
    ;
     binary_type("REL"),
     relocation(Src+Offset,_,_,_)
	;
     binary_type("EXEC"),
     instruction_get_operation(Src,Operation),
     !operation_not_for_address(Operation)
	).

may_have_symbolic_immediate(Src,Dest):-
    direct_jump(Src,Dest).

may_have_symbolic_immediate(Src,Dest):-
    direct_call(Src,Dest).

.decl operation_not_for_address(Operation:symbol)

operation_not_for_address("AND").
operation_not_for_address("XOR").

.decl invalid(EA:address)

// if the decoder failed to decode any instruction at EA, EA is invalid
invalid(EA):-
	invalid_op_code(EA).
	
// if EA contains a direct call or jump to something that cannot be code, it is invalid
invalid(EA):-
	(
	must_fallthrough(EA,Not_code);
	direct_jump(EA,Not_code);
	direct_call(EA,Not_code)
	//pc_relative_jump(EA,Not_code);
	//pc_relative_call(EA,Not_code)
	),
	!instruction(Not_code,_,_,_,_,_,_,_).
	
// propagate the invalid instructions backwards until ret or jmp (encoded in must_fallthrough)
// propagate through direct jumps and calls
invalid(From):-
	invalid(To),
	(
		must_fallthrough(From,To);
		direct_jump(From,To);
		direct_call(From,To);
		pc_relative_jump(From,To);
		pc_relative_call(From,To)
	).

.decl possible_ea(EA:address)

possible_ea(EA):-
	instruction(EA,_,_,_,_,_,_,_),
	!invalid(EA).

.decl basic_target(ea:address)

// FIXME: add exception frame addresses

basic_target(Val):-
	(
 	relative_address(_,_,Val)
	;
	binary_type("DYN"),
	address_in_data(EA,Val),
	relocation(EA,_,_,_)
	;
    binary_type("REL"),
    address_in_data(EA,Val),
    relocation(EA,_,_,_)
    ;
	binary_type("EXEC"),
	address_in_data(_,Val)
	;
	entry_point(Val)
	;
	code_section(Name),
	section(Name,_,Val)
	;
	function_symbol(Val,_)
	).

.decl block_limit(EA:address)

block_limit(EA):-
	(
	op_immediate(_,EA);
	pc_relative_operand(_,_,EA);
	basic_target(EA);
    instruction_has_loop_prefix(EA)
	).

// The targets are computed incrementally now as we traverse the code
// likely_ea and possible_target_from are mutually recursive

.decl possible_target_from(dest:address,src:address)

possible_target_from(Dest,Src):-
	code_in_block_candidate(Src,_),
	(
	may_have_symbolic_immediate(Src,Dest);
	pc_relative_operand(Src,_,Dest)
	).

.decl possible_target(Target:address)
.decl code_in_block_candidate(EA:address,EA_block:address)

possible_target(EA):-
	basic_target(EA).

possible_target(EA):-
	possible_target_from(EA,_).

// start a new block given a possible target
code_in_block_candidate(EA,EA):-
	possible_target(EA),
	possible_ea(EA).

// extend the block as long as we are sure to fallthrough and we have not
// reached a block limit
code_in_block_candidate(EA,Start):-
	code_in_block_candidate(EA2,Start),
	must_fallthrough(EA2,EA),
	!block_limit(EA).


// if reached a block limit or an instruction that does not necessarily
// fallthrough continue exploring but start a new block
code_in_block_candidate(EA,EA):-
	code_in_block_candidate(EA2,Start),
	may_fallthrough(EA2,EA),
	(
	  !must_fallthrough(EA2,EA)
	;
	  block_limit(EA)
	),
	possible_ea(EA).
	

// in case we cannot fallthrough, we skip any nops that we might find
// and generate a new possible_target once the nops finish
.decl after_end(EA:address,End:address)

after_end(Next,End):-
	code_in_block_candidate(End,_),
	(
	   unconditional_jump(End);
	   return(End);
	   halt(End)
	),
	next(End,Next).

after_end(Next,End):-
	after_end(EA,End),
	is_nop(EA),
	!block_limit(EA),
	next(EA,Next).

possible_target(EA):-
	after_end(EA,_),
	(
	!is_nop(EA);
	block_limit(EA)
	).
///////////////////////////////////////////////////////////////////////
// We are done with the recursive exploration
// Now we detect and resolve conflics within the different blocks

//First remove common tails
.decl code_in_block_candidate_refined(EA:address,Block:address)

code_in_block_candidate_refined(Block,Block):-
	code_in_block_candidate(Block,Block).

code_in_block_candidate_refined(EA_next,Block):-
	code_in_block_candidate_refined(EA,Block),
	next(EA,EA_next),
	code_in_block_candidate(EA_next,Block),
	!code_in_block_candidate(EA_next,EA_next).


.decl block_candidate_limit(EA:address,Limit:address)

block_candidate_limit(Block,Next):-
    code_in_block_candidate_refined(EA,Block),
    next(EA,Next),
    !code_in_block_candidate_refined(Next,Block).

// auxiliary predicate to detect that there are several candidates that fallthrough into a third candidate block
.decl multiple_fallthrough_to(Block:address)

multiple_fallthrough_to(Block):-
	may_fallthrough(EA,Block),
	code_in_block_candidate_refined(EA,Block_pred),
	may_fallthrough(EA2,Block),
	code_in_block_candidate_refined(EA2,Block_pred2),
	Block_pred!=Block_pred2.

.decl instruction_range(EA:address,EA_Instruction:address)

instruction_range(EA2+1,EA2):-
	code_in_block_candidate_refined(EA2,_),
	next(EA2,End),
	EA2+1 < End.
instruction_range(EA+1,EA2):-
	instruction_range(EA,EA2),
	next(EA2,End),
	EA+1 < End.

.decl block_overlap(ea:address,ea2:address)
.output block_overlap

block_overlap(EA_block,EA_block2):-
	code_in_block_candidate_refined(EA,EA_block),
	instruction_range(EA_block2,EA),
	code_in_block_candidate_refined(_,EA_block2).

.decl block_is_overlaping(block:address)

block_is_overlaping(Block):-
	(
	block_overlap(Block,_);
	block_overlap(_,Block)
	).
	
//////////////////////////////////////////////////////////////////////
// We need to solve the block overlaps
// we do it with heuristics
///

.decl discarded_block(ea_block:address)
.output discarded_block

discarded_block(Block):-
	(
	   block_overlap(Block,Block2);
	   block_overlap(Block2,Block)
	),
	// keep the block that has more points
	sum X:{block_points(Block,_,X,_)} < sum Y:{block_points(Block2,_,Y,_)}.

// propagate discarding blocks through direct jumps or calls
discarded_block(Block):-
	discarded_block(Block2),
	(
	direct_jump(EA,Block2);
	direct_call(EA,Block2)
	),
	code_in_block_candidate_refined(EA,Block).

/**
 The heuristics give a variable number of points for things like number
 of predecessors, how the block is reached (through direct jump, fallthough, etc.)
 and if the block's address appears in the data section, whether it appears aligned
 or in a pointer array.
 The points are trasmitted transitively though overlapping blocks.
*/

.decl block_points(block:address,predecessor:address,importance:number,why:symbol)
.output block_points


block_points(Block,0,0,"basic point"):-
	block_is_overlaping(Block),
	code_in_block_candidate_refined(_,Block).

block_points(Block,0,20,"start point"):-
	block_is_overlaping(Block),
	entry_point(Block).

block_points(Block,0,20,"function symbol"):-
	block_is_overlaping(Block),
	function_symbol(Block,_).
	
block_points(Block,0,1,"address in data"):-
	block_is_overlaping(Block),
	address_in_data(Address,Block),
	Address % 8 != 0.

block_points(Block,0,2,"aligned address in data"):-
	block_is_overlaping(Block),
	address_in_data(Address,Block),
	Address % 8 = 0.

block_points(Block,0,-1,"printable address"):-
	block_is_overlaping(Block),
	address_in_data(Address,Block),
	data_byte(EA,Byte),(printable_char(Byte); Byte=0),
	data_byte(EA+1,Byte1),(printable_char(Byte1); Byte1=0),
	data_byte(EA+2,Byte2),(printable_char(Byte2); Byte2=0),
	data_byte(EA+3,Byte3),(printable_char(Byte3); Byte3=0),
	data_byte(EA+4,Byte4),(printable_char(Byte4); Byte4=0),
	data_byte(EA+5,Byte5),(printable_char(Byte5); Byte5=0),
	data_byte(EA+6,Byte6),(printable_char(Byte6); Byte6=0),
	data_byte(EA+7,Byte7),(printable_char(Byte7); Byte7=0).

block_points(Block,0,1,"address in data array"):-
	block_is_overlaping(Block),
	address_in_data(Address,Block),
	Address % 8 = 0,
	(
	address_in_data(Address+8,_),
	address_in_data(Address+16,_)
	;
	address_in_data(Address-8,_),
	address_in_data(Address-16,_)
	;
	address_in_data(Address+8,_),
	address_in_data(Address-8,_)
	).

block_points(Block,Block_pred,6,"direct jump"):-
	block_is_overlaping(Block),
	direct_jump(EA,Block),
	code_in_block_candidate_refined(EA,Block_pred),
	Block_pred!=Block,
	!block_is_overlaping(Block_pred).

block_points(Block,Block_pred,6,"direct call"):-
	block_is_overlaping(Block),
	direct_call(EA,Block),
	code_in_block_candidate_refined(EA,Block_pred),
	Block_pred!=Block,
	!block_is_overlaping(Block_pred).
	
block_points(Block,Block_pred,2,"must-fallthrough"):-
	block_is_overlaping(Block),
	must_fallthrough(EA,Block),
	code_in_block_candidate_refined(EA,Block_pred),
	!block_is_overlaping(Block_pred).

block_points(Block,Block_pred,6,"may-fallthrough"):-
	block_is_overlaping(Block),
	may_fallthrough(EA,Block),
	code_in_block_candidate_refined(EA,Block_pred),
	!block_is_overlaping(Block_pred).

// it fits but the previous block does not fallthrough
block_points(Block,Block_pred,2,"fits"):-
	block_is_overlaping(Block),
	after_end(Block,EA),
	code_in_block_candidate_refined(EA,Block_pred),
	!block_is_overlaping(Block_pred).

// its address appears somewhere
block_points(Block,0,3,"appears"):-
	block_is_overlaping(Block),
	possible_target_from(Block,EA),
	!direct_jump(EA,Block),
  !direct_call(EA,Block),
	code_in_block_candidate_refined(EA,Block_pred),
	Block_pred!=Block,
	!block_is_overlaping(Block_pred).

block_points(Block,0,3,"in relative jump table"):-
	block_is_overlaping(Block),
	relative_address(_,_,Block).

// give a point if it may fallthrough to another block
block_points(Block,Block_succ,1,"fallthough_to"):-
	block_is_overlaping(Block),
	code_in_block_candidate_refined(EA,Block),
	may_fallthrough(EA,Block_succ),
	code_in_block_candidate_refined(_,Block_succ),
	!block_is_overlaping(Block_succ).

// transitivity for fallthrough and direct jumps and calls
block_points(Block,Block_pred2,Points,cat("fallthrough-transitive ",Why)):-
	block_is_overlaping(Block),
	may_fallthrough(EA,Block),
	!multiple_fallthrough_to(Block),
	code_in_block_candidate_refined(EA,Block_pred),
	block_is_overlaping(Block_pred),
	block_points(Block_pred,Block_pred2,Points,Why),
	Points>0.

block_points(Block,Block_pred2,Points-1,cat("multiple-fallthrough-transitive ",Why)):-
	block_is_overlaping(Block),
	may_fallthrough(EA,Block),
	multiple_fallthrough_to(Block),
	code_in_block_candidate_refined(EA,Block_pred),
	block_is_overlaping(Block_pred),
	block_points(Block_pred,Block_pred2,Points,Why),
	Points>1.

block_points(Block,Block_pred2,Points,"jump-transitive"):-
	block_is_overlaping(Block),
	direct_jump(EA,Block),
	code_in_block_candidate_refined(EA,Block_pred),
	block_is_overlaping(Block_pred),
	block_points(Block_pred,Block_pred2,Points,_).

block_points(Block,Block_pred2,Points,"call-transitive"):-
	block_is_overlaping(Block),
	direct_jump(EA,Block),
	code_in_block_candidate_refined(EA,Block_pred),
	block_is_overlaping(Block_pred),
	block_points(Block_pred,Block_pred2,Points,_).

block_points(Block,0,1,"has well-defined jump"):-
	block_is_overlaping(Block),
	direct_jump(EA,Block_dest),
	code_in_block_candidate_refined(EA,Block),
	code_in_block_candidate_refined(_,Block_dest),
	!block_is_overlaping(Block_dest).

block_points(Block,0,1,"has well-defined call"):-
	block_is_overlaping(Block),
	direct_call(EA,Block_dest),
	code_in_block_candidate_refined(EA,Block),
	code_in_block_candidate_refined(_,Block_dest),
	!block_is_overlaping(Block_dest).

block_points(Block,0,1,"plt call"):-
	block_is_overlaping(Block),
	direct_call(EA,Block_dest),
	code_in_block_candidate_refined(EA,Block),
	plt_entry(Block_dest,_).


////////////////////////////////////////////////////////////////////////////


code_in_block(EA,Block):-
	code_in_block_candidate_refined(EA,Block),
	!discarded_block(Block).

block_last_instruction(Block,EA):-
    code_in_block(EA,Block),
    next(EA,Next),
    !code_in_block(Next,Block).

block(Block):-
	code_in_block(_,Block).
	
code(EA):-
	code_in_block(EA,_).
	
block_still_overlap(Block,Block2):-
	block_overlap(Block,Block2),
	!discarded_block(Block),
	!discarded_block(Block2).
