//===- pointer_reatribution.dl ----------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
Compilers sometimes generate expressions of the form
symbol+constant. I can happen that such expression falls in the 1) middle
of a pointer, 2) outside the data sections or 3) falls into a data section
that is different from the one of symbol.  We want to detect those
cases and generate the adequate symbol+constant.

We generate two predicates:

-moved_data_label
 for pointers in data sections
-moved_label
 for pointers in code sections

We only 'move' pointers in data sections if their destination falls in
the middle of another pointer (case 1).  In code sections we consider
the tree possibilities.

In addition, we distinguish two types: I) the
pointer appears as an immediate operand or O) as an offset in an indirect
operand. We also consider a special case of indirect operand when we have
a pc-relative operand.

For indirect operands, we know they are being used to access memory. This makes
them more likely to be symbolic. They cannot be a float for example.
But they could still be a constant.

For immediate operands, they are likely to be symbolic if they are used to
compute an address but this is also no enough.

*/

.decl moved_data_label(EA:address,Dest:address,New_dest:address)
.output moved_data_label

.decl moved_label(EA:address,Index:number,Val:address,New_val:address)
.output moved_label

////////////////////////////////////////////////////////////////////////////////////

moved_data_label(EA,Dest,New_dest):-
	symbolic_data(EA,Dest),
	address_in_data_refined_range.overlap(Dest,8,New_dest).

//if something points to the middle of a known symbol we express it as symbol+constant
//as long as it is not code
moved_data_label(EA,Dest,Address):-
	symbolic_data(EA,Dest),
	!code(Dest),
	symbol(Address,Size,_,_,_,_),
	Address<Dest,
	Dest<Address+Size.

////////////////////////////////////////////////////////////////////////////////////


.decl address_comparison(EA_cmp:address,Address:address)

address_comparison(EA_cmp,Dest):-
	data_access_pattern(Dest,_,_,EA_used),
	!instruction_get_operation(EA_used,"LEA"),
	used_for_address(EA_cmp,Reg,EA_used,_),
	instruction_get_operation(EA_cmp,Operation),
	comparison_operation(Operation),
	instruction_get_src_op(EA_cmp,_,Op1),
	op_regdirect_contains_reg(Op1,Reg),
	!data_access_pattern(_,_,_,EA_cmp).


.decl related_data_access(EA:address,Dest:address)
.output related_data_access
// there is a LEA instruction that load some address in memory
// this address is used to access memory later somewhere else
related_data_access(EA_def,Dest):-
	address_comparison(EA_used,Dest),
	def_used(EA_def,_,EA_used,Index),
	(
	!data_access_pattern(_,_,_,EA_def)
	;
	instruction_get_operation(EA_def,"LEA")
	).
	


////////////////////////////////////////////////////////////////////////////////////

.decl moved_label_candidate(EA:address,Index:number,Val:address,New_val:address,Diff:number)

moved_label_candidate(EA,Op_index,Dest,New_dest,Diff):-
	code(EA),
	pc_relative_operand(EA,Op_index,Dest),
	!symbolic_operand(EA,Op_index),
	related_data_access(EA,Dest_access),
	Dest_access!=Dest,
	//section(Name,SectSize,Base),
	//Dest_access>=Base,
	//Dest_access<=SectSize+Base,
	New_dest=Dest_access,
	Diff=max(Dest_access-Dest,Dest-Dest_access).

moved_label_candidate(EA,Op_index,Dest,New_dest,Diff):-
	code(EA),
	pc_relative_operand(EA,Op_index,Dest),
	symbolic_operand(EA,Op_index),
	section(Name,SectSize,Base),
	Dest>=Base,
	Dest<=SectSize+Base,
	related_data_access(EA,Dest_access),
	(Dest_access<Base;Dest_access>Base+SectSize),//the access is to other section
	//get the correct section
	section(Name2,SectSize2,Base2),
	Dest_access>=Base2,
	Dest_access<=SectSize2+Base2,
	New_dest=Dest_access,
	Diff=max(Dest_access-Dest,Dest-Dest_access).
	
////////////////////////////////////////////////////////////////////////////////
// indirect operand (type O)

//pc-relative that misses the data sections
moved_label(EA,Op_index,Dest,New_dest):-
	moved_label_candidate(EA,Op_index,Dest,_,_),
	Diff_min=min X:moved_label_candidate(EA,Op_index,Dest,_,X),
	moved_label_candidate(EA,Op_index,Dest,New_dest,Diff),
	Diff<=Diff_min.

// label lands in the middle of a pointer
moved_label(EA,Op_index,Dest,New_dest):-
	symbolic_data_operand_candidate(EA,Op_index,Dest),
	data_access_pattern(New_dest,_,_,EA),
	(Dest>New_dest+3
	;
	Dest+3<New_dest
	),
	//it might collide with a pointer
	pointer_size(Pt_size),
	address_in_data_refined_range.overlap(Dest,Pt_size,_).

moved_label(EA,Op_index,Dest,New_dest):-
	symbolic_data_operand_candidate(EA,Op_index,Dest),
	//it collides with a pointer
	pointer_size(Pt_size),
	address_in_data_refined_range.overlap(Dest,Pt_size,New_dest),
	symbolic_data(New_dest,_).

	
// the pointer is likely to point to the wrong section
moved_label(EA,Op_index,Dest,New_dest):-
	symbolic_data_operand_candidate(EA,Op_index,Dest),
	instruction_get_op(EA,Op_index,Op),
	op_indirect(Op,_,_,_,_,_,_),
	data_access_pattern(Access_dest,_,Mult,EA),
	(
	Access_dest!=Dest,
	New_dest=Access_dest
	;
	Access_dest=Dest,
	New_dest=Dest+Mult
	),
	
	data_section(Name),
	section(Name,SectSize,Base),
	Dest>=Base,Dest<=Base+SectSize,

	regular_data_section(Name2), Name2!= Name,
	section(Name2,SectSize2,Base2),
	New_dest>=Base2,New_dest<=Base2+SectSize2,

	// sections can have one overlapping address
	Dest!=Base2,
	
	//we are going to move in the direction of the other section
	(
	Mult>0,New_dest>Dest
	;
	Mult<0, New_dest<Dest
	).


// labels that miss the data sections but should be symbolic
// the destination is before the data section
moved_label(EA,Op_index,Dest,Base):-
	code(EA),
	instruction_get_op(EA,Op_index,Op),
	!symbolic_operand(EA,Op_index),
	!pc_relative_operand(EA,Op_index,_),
	op_indirect(Op,_,_,_,_,Dest,_),
	Dest>40000,
	
	regular_data_section(Name),
	section(Name,_,Base),
	Dest<Base,
	Dest+4000>=Base.

// the destination is more than the end of the section
moved_label(EA,Op_index,Dest,Base+SizeSect):-
	code(EA),
	instruction_get_op(EA,Op_index,Op),
	!symbolic_operand(EA,Op_index),
	!pc_relative_operand(EA,Op_index,_),
	op_indirect(Op,_,_,_,_,Dest,_),
	data_access_pattern(Other_dest,_,_,EA),

	regular_data_section(Name),
	section(Name,SizeSect,Base),
	Other_dest>=Base,
	Other_dest<=Base+SizeSect,
	Dest>Base+SizeSect,
	Dest<=Base+SizeSect+(SizeSect/2).

////////////////////////////////////////////////////////////////////////////////
//immediate operand (type I)
//this considers also the case the pointer points to the wrong section (if the value is
// already symbolic

moved_label(EA,Op_index,Dest,New_dest):-
	code(EA),
	instruction_get_src_op(EA,Op_index,Op),
	op_immediate(Op,Dest),
	Dest>40000,
    //maybe add check that it points to a special section?
    (!symbolic_operand(EA,Op_index)
    ;
    symbolic_operand(EA,Op_index),
    data_section(Sect_origin),
    !regular_data_section(Sect_origin),
    section(Sect_origin,SizeSect_origin,Beg_origin),
    Dest>=Beg_origin,
    Dest<=SizeSect_origin+Beg_origin
    ),
	(
	  instruction_get_operation(EA,Operation),
	  comparison_operation(Operation)
	;
	  mov(EA)
	),
	instruction_get_op(EA,_,Op2),
	Op2!=Op,
	op_regdirect_contains_reg(Op2,Reg),

	local_next(EA,EA_next),
	used_for_address(EA_next,Reg,EA_used,Op_index_used),
	!symbolic_operand(EA_used,Op_index_used),

	//get the section where it is supposed to go
	(
	value_reg(EA_used,_,_,_,_,Other_dest,_)
	;
	// a related register
	def_used(EA_def,_,EA_used,_),
	value_reg(EA_def,_,_,_,_,Other_dest,_)
	),
	Other_dest!=0,
    	regular_data_section(Name),
        section(Name,SizeSect,Beg),
	Other_dest>=Beg,
	Other_dest<=SizeSect+Beg,
	(
	Dest< Beg,
	Dest+4000>=Beg,
	New_dest=Beg
	;
	Dest>SizeSect+Beg,
	Dest<=Beg+SizeSect+(SizeSect/2),
	New_dest=SizeSect+Beg
	).

// If the destination is in the middle of a known symbol
moved_label(EA,Op_index,Dest,Address):-
 	symbolic_data_operand_candidate(EA,Op_index,Dest),
 	symbol(Address,Size,_,_,_,_),
 	Address<Dest,
	Dest<Address+Size.
